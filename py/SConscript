# vim: set filetype=python :
Import('*')
linkPlugins=env['linkPlugins']
import os.path 

env.Install('$LIBDIR/py/yade',[
	env.SharedLibrary('WeightedAverage2d',['WeightedAverage2d.cpp'],SHLIBPREFIX=''),
	#env.SharedLibrary('_eudoxos',['_eudoxos.cpp'],SHLIBPREFIX='',LIBS=env['LIBS']),
	env.SharedLibrary('log',['log.cpp'],SHLIBPREFIX=''),
	#env.SharedLibrary('_utils',['_utils.cpp'],SHLIBPREFIX='',LIBS=env['LIBS']+[
	#	linkPlugins(['Shop','Facet']),
	#	]),
	env.SharedLibrary('_utils2',['_utils2.cpp'],SHLIBPREFIX='',LIBS=env['LIBS']+[linkPlugins(['Particle'])]),
	env.SharedLibrary('_packPredicates',['pack/_packPredicates.cpp'],SHLIBPREFIX='',
		# link to the symlink to the python module (created in lib/SConstruct; see explanation there)
		LIBS=env['LIBS']+(['_gts__python-module'] if 'YADE_GTS' in env['CPPDEFINES'] else []),
	),
	env.SharedLibrary('_packSpheres',['pack/_packSpheres.cpp'],SHLIBPREFIX=''),
	env.SharedLibrary('_packObb',['pack/_packObb.cpp'],SHLIBPREFIX=''),
	env.AlwaysBuild(env.ScanReplace('__init__.py.in')),
	env.AlwaysBuild(env.ScanReplace('config.py.in')),
	env.File(env.Glob('*.py')),
	env.File('pack/pack.py'),
	env.SharedLibrary('wrapper',['wrapper/wrapper.cpp'],SHLIBPREFIX=''), #,LIBS=linkPlugins(['Shop','Dispatching','InteractionLoop','ParallelEngine','Clump'])),
	env.SharedLibrary('_customConverters',['wrapper/customConverters.cpp'],SHLIBPREFIX='',LIBS=env['LIBS']) #+linkPlugins(['Dispatching','MatchMaker']+(['GLDrawFunctors','OpenGLRenderer'] if 'opengl' in env['features'] else [])))
])
env.Install('$LIBDIR/py/yade/tests',[
	env.File(env.Glob('tests/*.py'),'tests'),
])

# 3rd party modules:
# ==================
env.Install('$LIBDIR/py',[
	env.SharedLibrary('miniEigen',['mathWrap/miniEigen.cpp'],SHLIBPREFIX='',CPPPATH=env['CPPPATH']+['../lib/'],LIBS=env['LIBS']+['core']),
	env.File('mtTkinter.py','3rd-party/mtTkinter-0.3'),
])

if 'YADE_GTS' in env['CPPDEFINES']:
	env.Install('$LIBDIR/py/gts',[
		env.SharedLibrary('_gts',['3rd-party/pygts-0.3.1/cleanup.c','3rd-party/pygts-0.3.1/edge.c','3rd-party/pygts-0.3.1/face.c','3rd-party/pygts-0.3.1/object.c','3rd-party/pygts-0.3.1/point.c','3rd-party/pygts-0.3.1/pygts.c','3rd-party/pygts-0.3.1/segment.c','3rd-party/pygts-0.3.1/surface.c','3rd-party/pygts-0.3.1/triangle.c','3rd-party/pygts-0.3.1/vertex.c'],SHLIBPREFIX='',CPPDEFINES=env['CPPDEFINES']+['PYGTS_HAS_NUMPY'],CXXFLAGS=env['CXXFLAGS']+['-Wno-all']),
		env.File('3rd-party/pygts-0.3.1/__init__.py'),
		env.File('3rd-party/pygts-0.3.1/pygts.py')
	])
	#
	# the _packPredicates module needs to link to us, but linker by defaults looks for lib*.so files
	# Therefore we will create symlink with that name (in lib/ rather than py/gts, so that it is in the RPATH
	# without further modifications). If the file were copied (installed 2 times), static data would be
	# created twice and it would break python type identification (among other things).
	#
	env.Command('$LIBDIR/lib/lib_gts__python-module.so','$LIBDIR/py/gts/_gts.so','ln -s -f ../py/gts/_gts.so $TARGET')






