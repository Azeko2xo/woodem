Import('*')

if 'qt3' not in env['exclude']:
	# QGLVIewer uses non-standatd suffix of the generated .cpp file: standard is moc_VRenderInterface.Qt3.cpp, for example. This overrides that.	
	env.Uic(target=['QGLViewer/VRenderInterface.Qt3.h','QGLViewer/VRenderInterface.Qt3.cpp'],source='QGLViewer/VRenderInterface.Qt3.ui'),
	env.Uic(target=['QGLViewer/ImageInterface.Qt3.h','QGLViewer/ImageInterface.Qt3.cpp'],source='QGLViewer/ImageInterface.Qt3.ui'),
	env.Moc(source='QGLViewer/ImageInterface.Qt3.h',target=['QGLViewer/moc_ImageInterface.Qt3.cpp'])
	env.Moc(source='QGLViewer/VRenderInterface.Qt3.h',target=['QGLViewer/moc_VRenderInterface.Qt3.cpp'])
	env.Install('$PREFIX/lib/yade$SUFFIX/lib',[
		env.SharedLibrary('yade-serialization-qt',
			['serialization-qt/QtGUIGenerator.cpp'],
			LIBS=env['LIBS']+['yade-base','XMLFormatManager']),
		env.SharedLibrary('yade-QGLViewer',Split('''
				QGLViewer/moc_VRenderInterface.Qt3.cpp
				QGLViewer/moc_ImageInterface.Qt3.cpp

				QGLViewer/VRenderInterface.Qt3.cpp
				QGLViewer/ImageInterface.Qt3.cpp
				QGLViewer/qglviewer.cpp 
				QGLViewer/camera.cpp 
				QGLViewer/manipulatedFrame.cpp 
				QGLViewer/manipulatedCameraFrame.cpp 
				QGLViewer/frame.cpp 
				QGLViewer/saveSnapshot.cpp 
				QGLViewer/constraint.cpp 
				QGLViewer/keyFrameInterpolator.cpp 
				QGLViewer/mouseGrabber.cpp 
				QGLViewer/quaternion.cpp 
				QGLViewer/vec.cpp
				QGLViewer/VRender/BackFaceCullingOptimizer.cpp 
				QGLViewer/VRender/BSPSortMethod.cpp 
				QGLViewer/VRender/EPSExporter.cpp 
				QGLViewer/VRender/Exporter.cpp 
				QGLViewer/VRender/FIGExporter.cpp 
				QGLViewer/VRender/gpc.cpp 
				QGLViewer/VRender/ParserGL.cpp 
				QGLViewer/VRender/Primitive.cpp 
				QGLViewer/VRender/PrimitivePositioning.cpp 
				QGLViewer/VRender/TopologicalSortMethod.cpp 
				QGLViewer/VRender/VisibilityOptimizer.cpp 
				QGLViewer/VRender/Vector2.cpp 
				QGLViewer/VRender/Vector3.cpp 
				QGLViewer/VRender/NVector3.cpp 
				QGLViewer/VRender/VRender.cpp
				'''),
				# we want to REALLY optimize this, even in debug builds of yade
				CXXFLAGS=[x for x in env['CXXFLAGS'] if x not in ['-ggdb3','-g','-pg'] ]+['-O3','-g0'],
				# HACK: uic generates files in the buildDir, but gcc is invoked on files in the original dir;
				# since the generated header is #include'd "...", it looks for it in the original dir
				# no idea why this _does_ work with gui/qt3. Go figure.
				CPPPATH=env['CPPPATH']+['${TARGET.dir}']
			),
		])

env.Install('$PREFIX/lib/yade$SUFFIX/lib',[

	env.SharedLibrary('yade-base',
		['base/yadeWm3Extra.cpp'],
		LIBS=env['LIBS']),

	env.SharedLibrary('miniWm3',
		['miniWm3/Wm3Math.cpp',
			'miniWm3/Wm3Matrix3.cpp',
			'miniWm3/Wm3Quaternion.cpp',
			'miniWm3/Wm3Vector3.cpp',
			'miniWm3/Wm3Matrix2.cpp',
			'miniWm3/Wm3Matrix4.cpp',
			'miniWm3/Wm3Vector2.cpp',
			'miniWm3/Wm3Vector4.cpp',
			'miniWm3/Wm3Memory.cpp',
			'miniWm3/Wm3String.cpp',
			'miniWm3/Wm3System.cpp'],
		# miniWm3 cannot link with itself, filter it out.
		LIBS=filter(lambda l: l!='miniWm3',env['LIBS']),
		# miniWm3 will be always optimized and without debugging info, even in debug builds.
		CXXFLAGS=env['CXXFLAGS']+['-O3','-g0']),

	env.SharedLibrary('yade-computational-geometry',
		['computational-geometry/Distances2D.cpp',
			'computational-geometry/Distances3D.cpp',
			'computational-geometry/Intersections2D.cpp',
			'computational-geometry/Intersections3D.cpp',
			'computational-geometry/MarchingCube.cpp'],
		LIBS=env['LIBS']+['yade-base']),

	env.SharedLibrary('yade-factory',
		['factory/ClassFactory.cpp',
			'factory/DynLibManager.cpp',
			'factory/FactoryExceptions.cpp']),

	env.SharedLibrary('yade-loki',
		['loki/_dummySourceFile.cpp']),

	env.SharedLibrary('yade-multimethods',
		['multimethods/Indexable.cpp',
			'multimethods/MultiMethodsExceptions.cpp']),

	env.SharedLibrary('yade-opengl',
		['opengl/FpsTracker.cpp',
			'opengl/GLTextLabel.cpp',
			'opengl/GLWindow.cpp',
			'opengl/GLWindowsManager.cpp'],
		LIBS=env['LIBS']+['glut']),

	env.SharedLibrary('BINFormatManager',
		['serialization-bin/BINFormatManager.cpp']),


	env.SharedLibrary('XMLFormatManager',
		['serialization-xml/XMLFormatManager.cpp',
			'serialization-xml/XMLSaxParser.cpp'],
		LIBS=env['LIBS']+['yade-base']),

	env.SharedLibrary('yade-serialization',
		['serialization/Archive.cpp',
			'serialization/IOFormatManager.cpp',
			'serialization/IOManagerExceptions.cpp',
			'serialization/Serializable.cpp',
			'serialization/SerializableSingleton.cpp',
			'serialization/SerializationExceptions.cpp'])
])

