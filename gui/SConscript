# vim:syntax=python
Import('*')
linkPlugins=env['linkPlugins']

if 'qt4' in env['features']:
	env.Install('$PREFIX/lib/yade$SUFFIX/py/yade/qt',[
		env.File('qt4/img_rc.py'),
		env.File('qt4/ui_controller.py'),
		env.File('qt4/ui_SeqSerializable.py'),
		env.File('qt4/SerializableEditor.py'),
		env.File('qt4/__init__.py'),
		env.SharedLibrary('_GLViewer',['qt4/_GLViewer.cpp','qt4/OpenGLManager.cpp'],SHLIBPREFIX='',LIBS=env['LIBS']+['GLViewer'],RPATH=env['RPATH']+[env.Literal('\\$$ORIGIN/../../../gui')])
	])
	env.Command('qt4/img_rc.py','qt4/img.qrc','pyrcc4 -o $buildDir/gui/qt4/img_rc.py gui/qt4/img.qrc')
	env.Command('qt4/ui_controller.py','qt4/controller.ui','pyuic4 -o $buildDir/gui/qt4/ui_controller.py gui/qt4/controller.ui')
	env.Command('qt4/ui_SeqSerializable.py','qt4/SeqSerializable.ui','pyuic4 -o $buildDir/gui/qt4/ui_SeqSerializable.py gui/qt4/SeqSerializable.ui')
	env.Install('$PREFIX/lib/yade$SUFFIX/gui',[
		env.SharedLibrary('GLViewer',['qt4/GLViewer.cpp'],LIBS=env['LIBS']+[env['QGLVIEWER_LIB']]+linkPlugins(['OpenGLRenderer']))
	])
		

if 'opengl' in env['features'] and 'qt3' in env['features']:
	env.Install('$PREFIX/lib/yade$SUFFIX/gui',[
		env.SharedLibrary('QtGUI',
			['qt3/FileDialog.cpp',
				'qt3/GLViewer.cpp',
				'qt3/MessageDialog.cpp',
				'qt3/QtFileGenerator.cpp',
				'qt3/QtGUI.cpp',
				'qt3/QtGUIPreferences.cpp',
				'qt3/SimulationController.cpp',
				'qt3/YadeQtMainWindow.cpp',
				'qt3/YadeCamera.cpp',
				'qt3/QtGeneratedSimulationController.ui',
				'qt3/QtGeneratedMessageDialog.ui',
				'qt3/YadeQtGeneratedMainWindow.ui',
				'qt3/QtFileGeneratorController.ui'],
			LIBS=[# 'PythonUI',
				'yade-serialization-qt',
				linkPlugins(['GravityEngines','OpenGLRenderer']),
				env['QGLVIEWER_LIB']
			],
			CPPPATH=env['CPPPATH']+['qt3']
		),
		env.SharedLibrary('SnapshotEngine',['qt3/SnapshotEngine.cpp'],LIBS=env['LIBS']+['QtGUI','qglviewer-qt3']+linkPlugins(['PeriodicEngines']),CPPPATH=env['CPPPATH']+['qt3']),
	])

	env.Install('$PREFIX/lib/yade$SUFFIX/py/yade',[
		env.SharedLibrary('_qt',['qt3/QtGUI-python.cpp'],SHLIBPREFIX='',LIBS=['QtGUI'],CPPPATH=env['CPPPATH']+[env['buildDir']+'/gui/qt3']), # CPPPATH is for files generated by moc which are indirectly included
		env.File('qt.py','qt3'),
	])

	
	#
	# HACK that works around 
	#    https://bugs.launchpad.net/yade/+bug/406343
	#    https://bugs.launchpad.net/yade/+bug/582679				
	# Not clear why scons is not picking up the dependency automatically
	# Do not remove.
	#
	srcs=['FileDialog','GLViewer','MessageDialog','QtFileGenerator','QtGUI','QtGUIPreferences','SimulationController','YadeQtMainWindow','YadeCamera']
	uis=['QtGeneratedSimulationController','QtGeneratedMessageDialog','YadeQtGeneratedMainWindow',	'QtFileGeneratorController']
	import os.path
	for ui in uis:
		if os.path.exists(env.subst('$buildDir/gui/qt3/%s.h'%ui)): continue
		for s in srcs:
			env.AddPreAction('$buildDir/gui/qt3/%s.os'%s,'$QT_UIC -o $buildDir/gui/qt3/%s.h gui/qt3/%s.ui'%(ui,ui))

